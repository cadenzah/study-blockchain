## Operational Status Implementation - 실제 필요한 구현 대상들

- Boolean variable: `operational`
- Function modifier with `require()`: `requireIsOperational()`
  - 어떤 컨트랙트가 현재 작동 중인 상황인지 검사하는 불린 값
- Set value by contract owner: `setOperatingStatus()`
  - 컨트랙트 소유자 또는 허가받은 자가 컨트랙트를 통제할 수 있는 인터페이스 메서드
- 위에서 정의한 `requireIsOperational()` modifier를 실제 사용하는 컨트랙트 메서드들

## 앞으로의 실습에서 중요한 것

실습 내용 중 **잠재적인 버그 요인** 이 있다. 이를 파악하기 위한 고민을 해보자

## Exercise: Operational Status Control

### Add Operational Status Control to Smart Contract while avoiding a serious bug

- Make code changes to `ExerciseC6A.sol` and add tests as required
- Add a control flag `boolean` variable
- Add a function to enable the contract owner to change the control flag variable
- Use the control flag variable with `require()` in a function modifier
- Review functions that change state and add the modifier where appropriate

## Solutions

### (1) Add a control flag `boolean` variable

```solidity
bool private operational = true;
```

### (2) Add a function to enable the contract owner to change the control flag variable

```solidity
/**
* @dev Sets contract operations on/off
*
* When operational mode is disabled, all write transactions except for this one will fail
*/
function setOperatingStatus
                        (
                            bool mode
                        )
                        external
                        requireIsOperational
                        requireContractOwner
{
      operational = mode;
}
```

### (3) Use the control flag variable with `require()` in a function modifier

```solidity
/**
* @dev Modifier that requires the "operational" boolean variable to be "true"
*      This is used on all state changing functions to pause the contract in
*      the event there is an issue that needs to be fixed
*/
modifier requireIsOperational()
{
    require(operational, "Contract is currently not operational");
    _; // All modifiers require an "_" which indicates where the function body will be added
}
```

### Optional: helper function `isOperational()`

```solidity
/**
* @dev Get operating status of contract
*
* @return a bool that is the current operating status
*/
function isOperational()
                        public
                        view
                        returns(bool)
{
    return operational;
}
```

> 사용자를 등록할 때에 컨트랙트가 현재 작동중인지 체크해야 하므로, `registerUser()` 메서드에 `requireIsOperational` modifier를 추가해준다

## 위 코드의 잠재적인 버그??

위 코드는 `requireIsOperational` modifier를 통하여 컨트랙트가 작동 상태가 아니면, 모든 메서드의 수행을 멈춘다.

그렇다면, 작동을 끄는 것은 가능한데, 다시 켜는 것은 아예 불가능해지는 것이다. 즉, `setOperatingStatus`를 실행하여 컨트랙트를 다시 작동시키는 것은 불가능하다. 이렇게 되면 해당 컨트랙트는 다시는 작동시킬 수 없는 상태가 되버린다.

이러한 버그를 `Lockout` 버그라고 부른다.

### `Lockout`

The data state of the contract prevents access. → 위의 코드에서는 `operational` State 변수

### 교훈: Too Much를 조심하자

`Lockout` 버그는 컨트랙트를 보호하기 위하여 만든 코드로 인하여 발생하는 것이다. 코드 메커니즘을 잘 이해해서, 과한 보호가 되지 않도록 주의를 기울여야 한다.

`setOperatingStatus`에서 `requireIsOperational` modifier를 제거하면 해결.

## 정리

- Simple, but powerful technique for pausing Smart Contracts
- Avoidance of Lockout bug
- Reminder: *It is helpful to add tests for lockout scenarios*
  - 왜냐하면 깨닫고 난 뒤는 너무 늦어버리기 때문!!
