- 이제, 인터페이스를 기반으로 직접 토큰 컨트랙트를 만들어보자!

## Prerequisite

- 구현 방법1: 직접 모든 코드를 구현하기
- 에러 핸들링은 여기 포함되지 않음
  - 실제 네트워크에는 배포하면 안 됨
- 강연자는 22강의 구현 방법2를 더 선호하고 추천함
  - 이것은 원리 이해를 돕기 위함
- 여기에는 `allowance()`가 없음...
  - 단순히 이중 `mapping`을 구현하는 것으로 대체한듯

## 예제 코드

```sol
pragma solidity >=0.4.24;

contract myToken {

    string public constant name = "Udacity Token";
    string public constant symbol = "UDC";
    uint8 public constant decimals = 18;  // 18 is the most common number of decimal places
    uint _totalSupply; // 초기에 설정하는 토큰 총량

    // Balances for each account stored using a mapping
    // 강연자 왈 "mapping은 balance를 유지하는 가장 좋은 방법"
    mapping(address => uint256) balances;

    // Owner of the account approves the allowance of another account
    // Create an allowance mapping
    // The first key is the owner of the tokens
    // In the 2nd mapping, its says who can spend on your behalf, and how many
    // So, we are creating a mapping, where the key is an address,
    // The value is further a mapping of address to amount
    mapping(address => mapping (address => uint256)) allowance;
    // 첫번째 mapping의 key: 토큰의 본래 소유자 A의 계좌 주소
    // 두번째 mapping의 key: A를 대신하여 A의 토큰을 사용할 다른 계좌 B의 주소
    // 세번째 value: B가 사용할 수 있는 토큰의 양
    // 일종의 예치금 개념

    event Transfer(address indexed from, address indexed to, uint tokens);
    event Approval(address indexed tokenOwner, address indexed spender, uint tokens);


    // Called automatically when contract is initiated
    // Sets to total initial _totalSupply, as per the input argument
    // Also gives the initial supply to msg.sender...who creates the contract
    constructor(uint amount) public {
        _totalSupply = amount;
         balances[msg.sender] = amount;
         // 최초에는 모든 토큰을 해당 토큰 컨트랙트를 만든 사람이 보유하게 된다
    }

    // Returns the total supply of tokens
    function totalSupply() public view returns (uint256) {
        return _totalSupply;
    }

    // Get the token balance for account `tokenOwner`
    // Anyone can query and find the balance of an address
    function balanceOf(address tokenOwner) public constant returns (uint balance) {
        return balances[tokenOwner];
    }

    // Transfer the balance from owner's account to another account
    // Decreases the balance of "from" account
    // Increases the balance of "to" account
    // Emits Transfer event
    function transfer(address to, uint tokens) public returns (bool success) {
        if(tokens < 1){
            revert("Not enough Ether provided.");
        }
        require(tokens <= balances[msg.sender]);
        balances[msg.sender] = balances[msg.sender] - tokens;
        balances[to] = balances[to] + tokens;
        emit Transfer(msg.sender, to, tokens);
        return true;
    }

    // Send amount of tokens from address `from` to address `to`
    // The transferFrom method is used to allow contracts to spend
    // tokens on your behalf
    // Decreases the balance of "from" account
    // Decreases the allowance of "msg.sender"
    // Increases the balance of "to" account
    // Emits Transfer event
    function transferFrom(address from, address to, uint tokens) public returns (bool success) {
        balances[from] = balances[from] - tokens;
        allowance[from][msg.sender] = allowance[from][msg.sender] - tokens;
        balances[to] = balances[to] + tokens;
        emit Transfer(from, to, tokens);
        return true;
    }

    // Approves the `spender` to withdraw from your account, multiple times, up to the `tokens` amount.
    // So the msg.sender is approving the spender to spend these many tokens
    // from msg.sender's account
    // Setting up allowance mapping accordingly
    // Emits approval event
    function approve(address spender, uint tokens) public returns (bool success) {
        allowance[msg.sender][spender] = tokens;
        emit Approval(msg.sender, spender, tokens);
        return true;
    }
}
```
